#!/bin/csh

# @todo: dynamically find meta_model_root, i.e., this code base
set META_MODEL_ROOT="/opt/model/meta_model"

# Get params from comand arguments
source $META_MODEL_ROOT/model_get_args
echo $run_steps

# Load meta-model configuration
if ( -f $META_MODEL_ROOT/models/$model/model_config ) then
  source $META_MODEL_ROOT/models/$model/model_config
else 
  echo "Error: Can not find model configuration file $META_MODEL_ROOT/models/$model/model_config"
  echo "Exiting"
  exit
endif


echo "Beginning Run model=$model, scenario=$scenario, segment=$segment, work_path=$work_path, modules=$modules, run_steps=$run_steps,script=$script"

# todo: set up places to look for plugins in various trees such as MODEL_ROOT
set model_plugins=$META_MODEL_ROOT/models/$model
setenv MODEL_ROOT "$MODEL_ROOT"
setenv MODEL_BIN "$MODEL_BIN"
setenv META_MODEL_ROOT "$META_MODEL_ROOT"
setenv SCRIPT_DIR "$SCRIPT_DIR"
setenv MODEL "$model"
# iterate through different component of the model execution cycle
if ( -d $model_plugins ) then
  foreach module ( $modules )
    # todo: put checks in to see if this module is enabled in the run
    foreach step ( $run_steps )
      set script_path = "$model_plugins/$module/$step"
      if ( -d $script_path ) then 
        if ( $script  == "all" ) then
          set plugins = `ls $script_path`
        else 
          set plugins = "$script"
        endif
        if ( ! ( "$plugins" == "" ) ) then
          foreach plugin ( $plugins ) 
            echo "Running: $model_plugins/$module/$step/$plugin $scenario $segment $work_path"
            $model_plugins/$module/$step/$plugin $scenario $segment $work_path
          end
        else 
          echo "No model plugins found in $model_plugins/$module/$step/"
        endif
      else
        echo "No step $step found in $model/$module"
      endif
    end
  end
else
  echo "No model definitions found for model type $model in $META_MODEL_ROOT/models"
endif
